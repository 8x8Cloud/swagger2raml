import com.github.jengelman.gradle.plugins.shadow.transformers.ManifestResourceTransformer

apply plugin: 'groovy'
apply plugin: 'shadow'
apply plugin: 'maven'

group = 'com._8x8.cloud.swagger2raml'
version = '1.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
    }
}

if (project.hasProperty('smlNexusUser') && project.hasProperty('smlNexusPassword')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: 'https://nexus.softwaremill.com/content/repositories/snapshots') {
                    authentication(userName: smlNexusUser, password: smlNexusPassword)
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'http://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repository-master.mulesoft.org/releases/' }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile 'commons-cli:commons-cli:1.2'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.raml:raml-parser:0.8.4'
}

test {
    systemProperties 'file.encoding': 'UTF-8'
}

shadow {
    outputFile new File('dist/swagger2raml.jar')

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    transformer(ManifestResourceTransformer) {
        attributes "Main-Class": 'com._8x8.cloud.swagger2raml.RamlGenerator'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}